#version 430 core

layout(local_size_x = 64) in;

layout(std430, binding = 0) buffer VertexBuffer
{
    float data[];
};

uniform int uVertexCount;
uniform float uDisplacementScale;

const int VERTEX_SIZE = 16;
const int POS_OFFSET = 0;
const int NORMAL_OFFSET = 3;
const int UV_OFFSET = 6;
const int COLOR_RANGE_OFFSET = 8;
const int BARYCENTRIC_OFFSET = 10;
const int DISPLACEMENT_OFFSET = 13;

vec3 getPosition(uint idx)
{
    uint base = idx * VERTEX_SIZE;
    return vec3(data[base + POS_OFFSET], 
                data[base + POS_OFFSET + 1], 
                data[base + POS_OFFSET + 2]);
}

vec3 getDisplacement(uint idx)
{
    uint base = idx * VERTEX_SIZE;
    return vec3(data[base + DISPLACEMENT_OFFSET], 
                data[base + DISPLACEMENT_OFFSET + 1], 
                data[base + DISPLACEMENT_OFFSET + 2]);
}

void setNormal(uint idx, vec3 normal)
{
    uint base = idx * VERTEX_SIZE;
    data[base + NORMAL_OFFSET + 0] = normal.x;
    data[base + NORMAL_OFFSET + 1] = normal.y;
    data[base + NORMAL_OFFSET + 2] = normal.z;
}

void main()
{
    uint triangleIdx = gl_GlobalInvocationID.x;
    uint idx0 = triangleIdx * 3;
    uint idx1 = triangleIdx * 3 + 1;
    uint idx2 = triangleIdx * 3 + 2;
    
    if (idx2 >= uVertexCount)
        return;
    
    vec3 pos0 = getPosition(idx0) + getDisplacement(idx0) * uDisplacementScale;
    vec3 pos1 = getPosition(idx1) + getDisplacement(idx1) * uDisplacementScale;
    vec3 pos2 = getPosition(idx2) + getDisplacement(idx2) * uDisplacementScale;
    
    vec3 edge1 = pos1 - pos0;
    vec3 edge2 = pos2 - pos0;
    vec3 faceNormal = normalize(cross(edge2, edge1));
    
    setNormal(idx0, faceNormal);
    setNormal(idx1, faceNormal);
    setNormal(idx2, faceNormal);
}
